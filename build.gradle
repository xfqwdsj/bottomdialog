apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//apply plugin: 'com.novoda.bintray-release'

/*
ext {
    //bintray 网站上你创建的仓库的名字（必配项）
    bintrayRepo = 'bottomdialog'
    //在这个仓库下的 package name（必配项）
    bintrayName = 'bottomdialog'
    //以上两项均只是指向 bintray 网站上你的仓库和仓库下的package

    //publishedGroupId:artifact:libraryVersion 构成你开源库的唯一路径
    //例如：com.dasu.tv:tv:0.0.1，在build.gradle里就可以根据这个路径来compile依赖库了
    //以下三项均是必配项
    publishedGroupId = 'com.xfq.android'
    artifact = 'bottomdialog'
    libraryVersion = '1.0'

    //开源库对应的 github 地址，不知道可不可以不配，应该也是必配
    gitUrl = 'https://github.com/xfqwdsj/bottomdialog.git'

    //开发者信息，也是必配的吧
    developerId = 'dasu'
    developerName = 'dasu'
    developerEmail = '295207731@qq.com'
}





publish {
    userOrg = 'smalltmt6'
    groupId = 'com.xfq'//jcenter上的路径
    artifactId = 'bottomdialog'//项目名称
    publishVersion = '1.0'//版本号
    desc = 'Bottom dialog.'//描述，不重要
}

*/
android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    //implementation 'com.android.tools.build:gradle:4.0.1'
    //implementation 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    //implementation 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'


}

/** 以下开始是将Android Library上传到JCenter的相关配置**/

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目主页
def siteUrl = 'https://github.com/xfqwdsj/bottomdialog'
//项目的版本控制地址
def gitUrl = 'https://github.com/xfqwdsj/bottomdialog.git'

//发布到组织名称名字，必须填写
group = "com.xfq"
//发布到JCenter上的项目名字，必须填写
def libName = "bottomdialog"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0"
/**  上面配置后上传至JCenter后的编译路径是这样的： compile 'group:libName:version'  **/

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links "https://github.com/xfqwdsj/bottomdialog"
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'bottomdialog'
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id 'smalltmt6'
                        name 'Xiaofanqie'
                        email 'smalltmt6@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "bottomdialog"
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = 'bottomdialog'    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}